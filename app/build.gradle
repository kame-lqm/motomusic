apply plugin: 'com.android.application'
apply plugin: 'bugly'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        applicationId "com.kame.motomusic"
        minSdkVersion 19
        targetSdkVersion 25
        versionCode 1
        versionName "1.0.0"

        buildConfigField 'String', 'BUGLY_APP_ID', getLocalValue("BUGLY_APP_ID", true)

        manifestPlaceholders = [AMAP_KEY: getLocalValue("AMAP_KEY")]
    }

    signingConfigs {
        release {
            storeFile file("wangchenyan.keystore")
            storePassword getLocalValue("STORE_PASSWORD")
            keyAlias getLocalValue("KEY_ALIAS")
            keyPassword getLocalValue("KEY_PASSWORD")
        }
        debug {
            storeFile file("debug.keystore")
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }

    }
}

bugly {
    appId = getLocalValue("BUGLY_APP_ID")
    appKey = getLocalValue("BUGLY_APP_KEY")
}

def getLocalValue(String key) {
    return getLocalValue(key, false)
}

def getLocalValue(String key, boolean quot) {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def value = properties.containsKey(key) ? properties.get(key) : ""
    if (quot) {
        value = "\"" + value + "\"";
    }
    return value
}

ext {
    supportVersion = "25.3.1"
}

dependencies {
    compile files('libs/Msc.jar')
    compile files('libs/Sunflower.jar')
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.zhy:okhttputils:2.6.2'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'me.wcy:lrcview:2.0.0'
    compile 'com.tencent.bugly:crashreport_upgrade:1.2.5'
}
